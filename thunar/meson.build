thunar_sources = [
  'main.c',
  'thunar-abstract-dialog.c',
  'thunar-abstract-dialog.h',
  'thunar-abstract-icon-view.c',
  'thunar-abstract-icon-view.h',
  'thunar-action-manager.c',
  'thunar-action-manager.h',
  'thunar-application.c',
  'thunar-application.h',
  'thunar-browser.c',
  'thunar-browser.h',
  'thunar-chooser-button.c',
  'thunar-chooser-button.h',
  'thunar-chooser-dialog.c',
  'thunar-chooser-dialog.h',
  'thunar-chooser-model.c',
  'thunar-chooser-model.h',
  'thunar-clipboard-manager.c',
  'thunar-clipboard-manager.h',
  'thunar-column-editor.c',
  'thunar-column-editor.h',
  'thunar-column-model.c',
  'thunar-column-model.h',
  'thunar-compact-view.c',
  'thunar-compact-view.h',
  'thunar-component.c',
  'thunar-component.h',
  'thunar-dbus-service.c',
  'thunar-dbus-service.h',
  'thunar-deep-count-job.c',
  'thunar-deep-count-job.h',
  'thunar-details-view.c',
  'thunar-details-view.h',
  'thunar-device-monitor.c',
  'thunar-device-monitor.h',
  'thunar-device.c',
  'thunar-device.h',
  'thunar-dialogs.c',
  'thunar-dialogs.h',
  'thunar-dnd.c',
  'thunar-dnd.h',
  'thunar-emblem-chooser.c',
  'thunar-emblem-chooser.h',
  'thunar-enum-types.c',
  'thunar-enum-types.h',
  'thunar-file.c',
  'thunar-file.h',
  'thunar-folder.c',
  'thunar-folder.h',
  'thunar-gdk-extensions.c',
  'thunar-gdk-extensions.h',
  'thunar-gio-extensions.c',
  'thunar-gio-extensions.h',
  'thunar-gobject-extensions.c',
  'thunar-gobject-extensions.h',
  'thunar-gtk-extensions.c',
  'thunar-gtk-extensions.h',
  'thunar-history.c',
  'thunar-history.h',
  'thunar-ice.c',
  'thunar-ice.h',
  'thunar-icon-factory.c',
  'thunar-icon-factory.h',
  'thunar-icon-renderer.c',
  'thunar-icon-renderer.h',
  'thunar-icon-view.c',
  'thunar-icon-view.h',
  'thunar-image.c',
  'thunar-image.h',
  'thunar-io-jobs-util.c',
  'thunar-io-jobs-util.h',
  'thunar-io-jobs.c',
  'thunar-io-jobs.h',
  'thunar-io-scan-directory.c',
  'thunar-io-scan-directory.h',
  'thunar-job-operation-history.c',
  'thunar-job-operation-history.h',
  'thunar-job-operation.c',
  'thunar-job-operation.h',
  'thunar-job.c',
  'thunar-job.h',
  'thunar-location-bar.c',
  'thunar-location-bar.h',
  'thunar-location-button.c',
  'thunar-location-button.h',
  'thunar-location-buttons.c',
  'thunar-location-buttons.h',
  'thunar-location-entry.c',
  'thunar-location-entry.h',
  'thunar-menu.c',
  'thunar-menu.h',
  'thunar-navigator.c',
  'thunar-navigator.h',
  'thunar-notify.c',
  'thunar-notify.h',
  'thunar-pango-extensions.c',
  'thunar-pango-extensions.h',
  'thunar-path-entry.c',
  'thunar-path-entry.h',
  'thunar-permissions-chooser.c',
  'thunar-permissions-chooser.h',
  'thunar-preferences-dialog.c',
  'thunar-preferences-dialog.h',
  'thunar-preferences.c',
  'thunar-preferences.h',
  'thunar-private.h',
  'thunar-progress-dialog.c',
  'thunar-progress-dialog.h',
  'thunar-progress-view.c',
  'thunar-progress-view.h',
  'thunar-properties-dialog.c',
  'thunar-properties-dialog.h',
  'thunar-renamer-dialog.c',
  'thunar-renamer-dialog.h',
  'thunar-renamer-model.c',
  'thunar-renamer-model.h',
  'thunar-renamer-pair.c',
  'thunar-renamer-pair.h',
  'thunar-renamer-progress.c',
  'thunar-renamer-progress.h',
  'thunar-sendto-model.c',
  'thunar-sendto-model.h',
  'thunar-session-client.c',
  'thunar-session-client.h',
  'thunar-shortcuts-icon-renderer.c',
  'thunar-shortcuts-icon-renderer.h',
  'thunar-shortcuts-model.c',
  'thunar-shortcuts-model.h',
  'thunar-shortcuts-name-renderer.c',
  'thunar-shortcuts-name-renderer.h',
  'thunar-shortcuts-pane.c',
  'thunar-shortcuts-pane.h',
  'thunar-shortcuts-view.c',
  'thunar-shortcuts-view.h',
  'thunar-side-pane.c',
  'thunar-side-pane.h',
  'thunar-simple-job.c',
  'thunar-simple-job.h',
  'thunar-size-label.c',
  'thunar-size-label.h',
  'thunar-standard-view.c',
  'thunar-standard-view.h',
  'thunar-statusbar.c',
  'thunar-statusbar.h',
  'thunar-text-renderer.c',
  'thunar-text-renderer.h',
  'thunar-thumbnail-cache.c',
  'thunar-thumbnail-cache.h',
  'thunar-thumbnailer.c',
  'thunar-thumbnailer.h',
  'thunar-toolbar-editor.c',
  'thunar-toolbar-editor.h',
  'thunar-transfer-job.c',
  'thunar-transfer-job.h',
  'thunar-tree-model.c',
  'thunar-tree-model.h',
  'thunar-tree-pane.c',
  'thunar-tree-pane.h',
  'thunar-tree-view-model.c',
  'thunar-tree-view-model.h',
  'thunar-tree-view.c',
  'thunar-tree-view.h',
  'thunar-user.c',
  'thunar-user.h',
  'thunar-util.c',
  'thunar-util.h',
  'thunar-view.c',
  'thunar-view.h',
  'thunar-window.c',
  'thunar-window.h',
]
# Add terminal widget sources conditionally
if vte.found()
  thunar_sources += ['thunar-terminal-widget.c', 'thunar-terminal-widget.h']
endif

thunar_sources += gnome.genmarshal(
  'thunar-marshal',
  sources: 'thunar-marshal.list',
  prefix: '_thunar_marshal',
  internal: true,
  install_header: false,
)

services = {
  'thunar-dbus-freedesktop-interfaces.xml': 'thunar-dbus-freedesktop-interfaces',
  'thunar-dbus-service-infos.xml': 'thunar-dbus-service-infos',
  'thunar-thumbnail-cache-dbus.xml': 'thunar-thumbnail-cache-proxy',
  'thunar-thumbnailer-dbus.xml': 'thunar-thumbnailer-proxy',
}
foreach xml_file, basename : services
  thunar_sources += gnome.gdbus_codegen(
    basename,
    sources: xml_file,
    namespace: 'Thunar',
    install_header: false,
  )
endforeach

thunar_sources += gnome.compile_resources(
  'thunar.gresource',
  'thunar.gresource.xml',
)

executable(
  'thunar',
  thunar_sources,
  sources: xfce_revision_h,
  c_args: [
    '-DG_LOG_DOMAIN="@0@"'.format('thunar'),
    '-D_XOPEN_SOURCE=700', # for strptime in time.h
  ],
  include_directories: [
    include_directories('..'),
  ],
  dependencies: [
    gio,
    gio_unix,
    gthread,
    gtk,
    libxfce4ui,
    libxfce4kbd,
    libxfce4util,
    xfconf,
    x11_deps,
    gudev,
    libnotify,
    pango,
    vte
  ],
  link_with: [
    libthunarx,
  ],
  install: true,
  install_dir: get_option('prefix') / get_option('bindir'),
)

i18n.merge_file(
  input: 'thunar-settings.desktop.in',
  output: 'thunar-settings.desktop',
  po_dir: '..' / 'po',
  type: 'desktop',
  install: true,
  install_dir: get_option('prefix') / get_option('datadir') / 'applications',
)

install_symlink(
  'Thunar',
  install_dir: get_option('prefix') / get_option('bindir'),
  pointing_to: 'thunar',
)

install_data(
  'thunar-settings',
  install_dir: get_option('prefix') / get_option('bindir'),
  install_mode: 'rwxr-xr-x',
)
